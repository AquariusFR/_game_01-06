<div style="position: relative">
    <canvas class="game__canvas__background"></canvas>
    <canvas id="sprites" class="game__canvas__sprites"></canvas>
    <div>
        <button (click)="zoomOut()">-</button>
        <button (click)="zoomReset()">=</button>
        <button (click)="zoomIn()">+</button>
    </div>
    <div>
        <button (click)="moveLeft()">⇦</button>
        <button (click)="moveRight()">⇨</button>
        <button (click)="moveUp()">⇧</button>
        <button (click)="moveDown()">⇩</button>
    </div>
</div>


<script id="2d-vertex-shader" type="x-shader/x-vertex">
    attribute vec2 a_position;

    uniform vec2 u_resolution;
    uniform vec2 u_translation;
    uniform vec2 u_scale;

    attribute vec2 a_texCoord;
    varying vec2 v_texCoord;
    

    void main() {
        // convert the rectangle from pixels to 0.0 to 1.0
        
        vec2 scaledPosition = a_position * u_scale;
        vec2 position = scaledPosition + u_translation;
        
        vec2 zeroToOne = position / u_resolution;

        // convert from 0->1 to 0->2
        vec2 zeroToTwo = zeroToOne * 2.0;

        // convert from 0->2 to -1->+1 (clipspace)
        vec2 clipSpace = zeroToTwo - 1.0;

        gl_Position = vec4(clipSpace * vec2(1, -1), 0, 1);

        // pass the texCoord to the fragment shader
        // The GPU will interpolate this value between points
        v_texCoord = a_texCoord;
    }
</script>
<script id="2d-fragment-shader" type="x-shader/x-fragment">
    precision mediump float;
    // uniform vec4 u_color;
    // our texture
    uniform sampler2D u_image;
    // the texCoords passed in from the vertex shader.
    varying vec2 v_texCoord;
    uniform vec2 u_textureSize;

    void main() {
        vec2 onePixel = vec2(1.0, 1.0) / u_textureSize;
        //gl_FragColor = u_color;
        gl_FragColor = texture2D(u_image, v_texCoord);

        
 
   // average the left, middle, and right pixels.
/*   gl_FragColor = (
       texture2D(u_image, v_texCoord) +
       texture2D(u_image, v_texCoord + vec2(onePixel.x, 0.0)) +
       texture2D(u_image, v_texCoord + vec2(-onePixel.x, 0.0))) / 3.0;*/
    }
</script>

<script id="spriteVertexShader" type="x-shader/x-vertex">
uniform float u_frameOffset;

// Corrects for screen size.
uniform vec4 u_screenDims;

// Center of the sprite in screen coordinates
attribute vec2 centerPosition;

attribute float rotation;

// Per-sprite frame offset.
attribute float perSpriteFrameOffset;

// Sprite size in screen coordinates
attribute float spriteSize;

// Offset of this vertex's corner from the center, in normalized
// coordinates for the sprite. In other words:
//   (-0.5, -0.5) => Upper left corner
//   ( 0.5, -0.5) => Upper right corner
//   (-0.5,  0.5) => Lower left corner
//   ( 0.5,  0.5) => Lower right corner
attribute vec2 cornerOffset;

// Note: we currently assume that all sprite sheets start from the
// upper-left corner (which we define as (0,0)). Simply add another
// attribute float for the Y start of the sheet's upper left corner to
// add support for packing multiple sheets onto a single texture.

// Specified in normalized coordinates (0.0..1.0).
attribute vec2 spriteTextureSize;

attribute float spritesPerRow;
attribute float numFrames;

// For now we fix the number of textures the atlas can handle to 4.
// We could improve this by generating the shader code and passing
// down a varying array. Each element in this vec4 is either 0.0 or
// 1.0, with only one 1.0 entry, and essentially selects which texture
// will be displayed on the sprite.
attribute vec4 textureWeights;

// Output to the fragment shader.
varying vec2 v_texCoord;
varying vec4 v_textureWeights;

void main() {
  // Compute the frame number
  float frameNumber = mod(u_frameOffset + perSpriteFrameOffset, numFrames);
  // Compute the row
  float row = floor(frameNumber / spritesPerRow);
  // Compute the upper left texture coordinate of the sprite
  vec2 upperLeftTC = vec2(spriteTextureSize.x * (frameNumber - (row * spritesPerRow)),
                          spriteTextureSize.y * row);
  // Compute the texture coordinate of this vertex
  vec2 tc = upperLeftTC + spriteTextureSize * (cornerOffset + vec2(0.5, 0.5));
  v_texCoord = tc;
  v_textureWeights = textureWeights;

  float s = sin(rotation);
  float c = cos(rotation);
  mat2 rotMat = mat2(c, -s, s, c);
  vec2 scaledOffset = spriteSize * cornerOffset;
  vec2 pos = centerPosition + rotMat * scaledOffset;
  gl_Position = vec4(pos * u_screenDims.xy + u_screenDims.zw, 0.0, 1.0);
}
</script>
<script id="spriteFragmentShader" type="x-shader/x-fragment">
precision mediump float;

// Arrays of uniform samplers are currently problematic on some platforms.
// For now, convert them to individual uniforms.
uniform sampler2D u_texture0;
uniform sampler2D u_texture1;
uniform sampler2D u_texture2;
uniform sampler2D u_texture3;

varying vec2 v_texCoord;
varying vec4 v_textureWeights;

void main() {
  // Note: this fragment shader was originally written as:
  //  gl_FragColor = (texture2D(u_texture0, v_texCoord) * v_textureWeights.x +
  //                  texture2D(u_texture1, v_texCoord) * v_textureWeights.y +
  //                  texture2D(u_texture2, v_texCoord) * v_textureWeights.z +
  //                  texture2D(u_texture3, v_texCoord) * v_textureWeights.w);
  //
  // in order to avoid using the if-statement, under the supposition
  // that using branches would perform worse than a straight-line
  // statement. (Using an array of samplers is not an option for this
  // use case in OpenGL ES SL and therefore WebGL shaders.) It turns
  // out that at least on an NVIDIA GeForce 8000 series card, the
  // if-statements are massively faster, because the untaken texture
  // fetches can be eliminated, so a huge amount of texture bandwidth
  // is saved. Many thanks to Nat Duca for this suggestion.

  vec4 color;
  if (v_textureWeights.x > 0.0)
    color = texture2D(u_texture0, v_texCoord);
  else if (v_textureWeights.y > 0.0)
    color = texture2D(u_texture1, v_texCoord);
  else if (v_textureWeights.z > 0.0)
    color = texture2D(u_texture2, v_texCoord);
  else // v_textureWeights.w > 0.0
    color = texture2D(u_texture3, v_texCoord);
  gl_FragColor = color;
}
</script>